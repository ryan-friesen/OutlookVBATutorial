    '1. Go to File > Options > Trust Center > Trust Center Settings > Macro Settings, then select "Notifications for all macros". This allows the macros to run after you select "Enable all Macros" when Outlook opens
    '2. Press alt + F11 with the Outlook window in focus, or enable the developer tab in File > Options > Customize Ribbon to access the "Microsoft Visual Basic for Applications" window
    '3. Open the Visual Basic for Applications window, then navigate to View > Project Explorer to open the VbaProject.OTM window
    '4. Expand VbaProject.OTM, right-click on "Microsoft Outlook Projects" and select Insert > Module
    '5. Paste this script into the window, input your Folders and filePath value below
    '6. Navigate to File > Save VbaProject.OTM
    '7. Install Notepad++, then navigate to Preferences > MISC. > Enable File Status Auto-Detection and check the "Update silently" box so that the window dynamically updates


Private WithEvents inboxItems As Outlook.Items

Private Sub Application_Startup()
    Dim outlookNamespace As Outlook.NameSpace
    Dim targetFolder As Outlook.Folder

    Set outlookNamespace = Application.GetNamespace("MAPI")
    Set targetFolder = outlookNamespace.GetDefaultFolder(olFolderInbox).Folders([DL name])
    Set inboxItems = targetFolder.Items
End Sub

Private Sub inboxItems_ItemAdd(ByVal Item As Object)
    If TypeOf Item Is Outlook.MailItem Then
        Dim mail As Outlook.MailItem
        Set mail = Item

        If mail.MessageClass <> "IPM.Note" Then Exit Sub

        Dim filePath As String
        filePath = "C:\Users\[username]\Desktop\[filename].txt"
        Dim senderName As String
        senderName = mail.senderName
        Dim line As String
        Dim updatedContent As String
        Dim newLine As String
        Dim todayDate As String

        ' Today's date for comparison
        todayDate = Format(Date, "mm/dd/yyyy")

        ' New entry with date included
        newLine = senderName & " | " & mail.Subject & " | " & Format(mail.ReceivedTime, "mm/dd/yyyy hh:mm AM/PM") & vbCrLf & vbCrLf

        ' Reads the existing file and keeps only today's entries
        If Dir(filePath) <> "" Then
            Open filePath For Input As #1
            Do Until EOF(1)
                Line Input #1, line
                ' Keep line only if it contains today's date
                If InStr(line, todayDate) > 0 Then
                    updatedContent = updatedContent & line & vbCrLf
                End If
            Loop
            Close #1
        End If

        ' Adds the new line at the top
        updatedContent = newLine & updatedContent

        ' Writes updated content back to txt file
        Open filePath For Output As #1
        Print #1, updatedContent
        Close #1

        ' Open or refresh in Notepad++
        Shell """C:\Program Files\Notepad++\notepad++.exe"" " & filePath, vbNormalFocus
    End If
End Sub
